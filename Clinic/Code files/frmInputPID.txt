'General
Option Explicit

Dim rs As ADODB.Recordset
Dim cn As ADODB.Connection
Public PIDValue As String

Private Sub Form_Unload(Cancel As Integer)

    If Not rs Is Nothing Then
        If rs.State = adStateOpen Then rs.Close
    End If

    If Not cn Is Nothing Then
        If cn.State = adStateOpen Then cn.Close
    End If

End Sub

Private Sub Form_Load()
    Set cn = New ADODB.Connection
    cn.Open "Provider=Microsoft.Jet.OLEDB.4.0;Data Source=" & _
            App.Path & "\ClinicRecord.mdb"
End Sub

Private Sub txtPID_KeyPress(KeyAscii As Integer)
    If Not (KeyAscii >= 48 And KeyAscii <= 57) And KeyAscii <> 8 Then
        KeyAscii = 0
    End If
End Sub

Private Sub cmdConfirm_Click()
    If Trim(txtPID.Text) = "" Then
        MsgBox "Please enter Patient ID.", vbExclamation
        Exit Sub
    End If

    If Not IsNumeric(txtPID.Text) Then
        MsgBox "Patient ID must contain numbers only.", vbExclamation
        Exit Sub
    End If
    Set rs = New ADODB.Recordset
    
    rs.Open "SELECT ID FROM patient_master WHERE ID = " & CLng(txtPID.Text), _
                 cn, adOpenStatic, adLockReadOnly

    If rs.EOF Then
        MsgBox "No Patient ID Found.", vbCritical
        rs.Close
        Set rs = Nothing
        txtPID.SetFocus
        Exit Sub
    End If

    rs.Close
    Set rs = Nothing

    PIDValue = Trim(txtPID.Text)
    Me.Hide
End Sub

Private Sub cmdCancel_Click()
    PIDValue = ""
    Unload Me
End Sub


